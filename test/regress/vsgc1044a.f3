/**
 * Regression test for VSGC-1044: KeyFrame.action functions are not invoked in proper time
 *   (for one cycle only)
 *
 * @test/nocompare
 * @run
 */

import f3.animation.*;
import f3.lang.Duration;
import java.lang.System;
import java.lang.AssertionError;

function runLater(ms: Duration, f: function(): Void): Void {
    Timeline {
        keyFrames: KeyFrame {
            time: ms
            action: f
        }
    }.play();
}

var keepAlive : Timeline = Timeline {
    repeatCount: Timeline.INDEFINITE
    keyFrames: KeyFrame {
        time: 100ms
    }
};

var golden: Number[] = [[0.0..20.0], 25.0];
var out: Number[];

class CLS {
    public var a:Number = -1;
}

var cls = CLS {};

function makeKF(n:Number):KeyFrame {
    KeyFrame {
        time: Duration.valueOf(n)
        values: cls.a => n
        action: function() {
            insert cls.a into out;
            //System.out.println("timeline tick {n}ms - {cls.a}");
        }
    }
}

var t: Timeline = Timeline {
    keyFrames: [
        for (n in [0..20]) makeKF(n),
        KeyFrame {
            time: 25ms
            values: cls.a => 25.0
            action: function() {
                insert cls.a into out;
                //System.out.println("timeline finished - {cls.a}");
            }
        }
    ]
};

keepAlive.play();
t.play();

runLater(1s, check);
function check() {    
    keepAlive.stop();
    if(t.running) {
        t.stop();
        throw new AssertionError("test failed: t is still running");
    }

    if(out != golden) {
        throw new AssertionError("test failed: {out} != {golden}");
    }
    System.out.println("pass");
}
